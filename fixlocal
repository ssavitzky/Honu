#!/bin/csh -f
#
# 	Fix various things, mostly X-ish, on sparcstations.
#	This should also work for setting up a new one.
#	MUST BE RUN AS ROOT
#
# Not Handled:
#	routed hack for access to/from giggles
#	fstab -- assume it's been rcp'd from somewhere.
#	doesn't rdist X11 if it's missing.  Do that on sol.
#	Automount should use the tables.
#	/usr/local -> /home/local -- really needs minimal emacs set up
#				     UNDERNEATH the mounted /usr/local
#	When we get the second disk set up on the sparcstations,
#	     it ought to be mounted on /home/`hostname`


set path = (/usr/bin /bin /usr/ucb /etc /usr/etc)
umask 2

# Everything Xish except the libraries belongs in /home/X11
# This, in turn, has to be in /usr/export on a 100Mb sparcstation.
#
if (! -e /home/X11) then
   if (-e /usr/export/home) then
      echo '-->Installing X11 in /usr/export'
      mkdir      /usr/export/X11
      chgrp xwin /usr/export/X11
      ln -s	 /usr/export/X11 /home/X11
   else
      echo '-->Installing X11 in /home'
      mkdir      /home/X11
      chgrp xwin /home/X11
   endif
   mkdir      /home/X11/usr /home/X11/usr/{bin,lib,include}
   chgrp xwin /home/X11/usr /home/X11/usr/{bin,lib,include}
   if ( -e /usr/bin/X11 ) then
      echo '-->Moving existing X11 directories to /home/X11/usr/*'
      # NOTE: won't work if /home and /usr are in separate filesystems.
      #	      It's ok for the current setup, however.
      mv /usr/bin/X11	  	/home/X11/usr/bin/X11
      mv /usr/lib/X11		/home/X11/usr/lib/X11
      mv /usr/include/X11	/home/X11/usr/include/X11
   endif
   ln -s /home/X11/usr/bin/X11		/usr/bin
   ln -s /home/X11/usr/lib/X11		/usr/lib
   ln -s /home/X11/usr/include/X11	/usr/include
endif

umask 22
# If we're not already exporting our local filesystems, do it here.
# (Note that the sparcstations only have / and /usr at present).
# While we're at it, start up the nfs server daemon
#
if (! -e /etc/exports ) then
   echo '-->Initializing /etc/exports'
   echo	'/	-root=sol:luna:sarc:arc'	>> /etc/exports
   echo	'/usr	-root=sol:luna:sarc:arc'	>> /etc/exports
   if ( { grep /home /etc/fstab } ) then
      echo '-->Adding /home entry to /etc/exports'
      echo '/home	-root=sol:luna:sarc:arc'	>> /etc/exports
   endif
   exportfs -a
   nfsd 8 &
   rpc.mountd -n
endif

# Make sure we automount on /net
# (This really ought to be done via the tables...)
#
if (! { grep 'automount /net' /etc/rc.local } ) then
   echo '-->Adding "automount /net -hosts" to /etc/rc.local'
   echo '#'		        >> /etc/rc.local
   echo 'automount /net -hosts' >> /etc/rc.local
   automount /net -hosts
endif

